# -------------------------------------------------------------------------------
# Load completion system
# -------------------------------------------------------------------------------

autoload -Uz compinit
compinit

# -------------------------------------------------------------------------------
# Set auto change directory
# -------------------------------------------------------------------------------

setopt auto_cd
setopt inc_append_history
setopt share_history

# -------------------------------------------------------------------------------
# Environment 
# -------------------------------------------------------------------------------

export EDITOR="nvim"
export VISUAL="nvim"

export DOCKER_BUILDKIT=1

# -------------------------------------------------------------------------------
# Aliases 
# -------------------------------------------------------------------------------

alias hload="fc -R $HOME/.zsh_history"
alias rload="source $HOME/.zshrc"
alias rmtar="rm -rf *.tar.gz"
alias nv="nvim"
alias rm="rm -rf"
alias mkdir="mkdir -p"
alias ll="ls -al --group-directories-first"
alias l="ls --color=tty --group-directories-first"
alias shutdown="shutdown -h now"
alias grep="grep --color=auto --exclude-dir={.bzr,CVS,.git,.hg,.svn}"
alias cx="chmod +x *.sh"

# Navigation
alias t1="tree --dirsfirst -C -L 1 ."
alias t2="tree --dirsfirst -C -L 2 ."
alias t3="tree --dirsfirst -C -L 3 ."
alias t4="tree --dirsfirst -C -L 4 ."

# Configuration
alias dz="nvim $HOME/.zshrc"
alias de="nvim $HOME/.zshenv"
alias di="nvim $HOME/.config/i3/config"
alias dn="cd $HOME/.config/nvim && nvim"
alias da="nvim $HOME/.config/alacritty/alacritty.yml"

# -------------------------------------------------------------------------------
# Update path variable
# -------------------------------------------------------------------------------

# User env vars
path+="$HOME/.local/bin"
path+="$HOME/.cargo/bin"
path+="$HOME/go/bin"
path+="$HOME/lua/lua-language-server/bin"

# -------------------------------------------------------------------------------
# Plugins
# -------------------------------------------------------------------------------

# https://github.com/zsh-users/zsh-completions
fpath+="$HOME/.zsh/zsh-completions"

# https://github.com/rupa/z
source "$HOME/.zsh/z/z.sh"
# https://github.com/zsh-users/zsh-autosuggestions
source "$HOME/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh"
# https://github.com/zsh-users/zsh-history-substring-search 
source "$HOME/.zsh/zsh-history-substring-search/zsh-history-substring-search.zsh"

# -------------------------------------------------------------------------------
# Command Prompt
# -------------------------------------------------------------------------------

# https://salferrarello.com/zsh-git-status-prompt/

# Autoload zsh add-zsh-hook and vcs_info functions (-U autoload w/o substition, -z use zsh style)
autoload -Uz add-zsh-hook vcs_info
# Enable substitution in the prompt.
setopt prompt_subst
# Run vcs_info just before a prompt is displayed (precmd)
add-zsh-hook precmd vcs_info

# Config for prompt. PS1 synonym.
prompt=$'\n%B%F{240}${vcs_info_msg_0_}%f%b\n'
prompt+=$'%F{242}%~%f %F{cyan}>%f '

# Prompt the error codes
# prompt+=$'%B%(?.%F{green}*.%F{red}* [%?])%f%b %F{242}%~%f %F{cyan}>%f '

# Enable checking for (un)staged changes, enabling use of %u and %c
zstyle ':vcs_info:*' check-for-changes true
# Set custom strings for an unstaged changes (*)
zstyle ':vcs_info:*' unstagedstr ' %F{red}*%f'
# Set custom strings for staged changes (+)
zstyle ':vcs_info:*' stagedstr ' %F{green}+%f'
# Set the format of the git information for vcs_info
zstyle ':vcs_info:git:*' formats '(%b%m%u%c%F{240})%f'
# Used when the prompt is displayed and git is in the middle of an action (like rebase, merge, or cherry-pick)
zstyle ':vcs_info:git:*' actionformats '(%b|%a%u%c)'

# -------------------------------------------------------------------------------
# Vim Mode
# -------------------------------------------------------------------------------

bindkey -v
# Make switch between vim modes faster
export KEYTIMEOUT=1
# Gives access to menuselect
zmodload zsh/complist

# Key bindings 
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# A visual indicator to show the current mode
function cursor_mode() {

    cursor_block='\e[2 q'
    cursor_beam='\e[6 q'

    function zle-keymap-select {
        if [[ ${KEYMAP} == vicmd ]] || [[ $1 = 'block' ]]; then
            echo -ne $cursor_block
        elif [[ ${KEYMAP} == main ]] || [[ ${KEYMAP} == viins ]] || [[ ${KEYMAP} = '' ]] || [[ $1 = 'beam' ]]; then
            echo -ne $cursor_beam
        fi
    }

    zle-line-init() {
        echo -ne $cursor_beam
    }

    zle -N zle-keymap-select
    zle -N zle-line-init
}

cursor_mode
